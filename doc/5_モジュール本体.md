# 5 モジュール本体

モジュール本体（セクション 4.2）には、本仕様で定義する VBA プログラミング言語の構文で記述されたソースコードが含まれる。本章では、モジュール本体の有効な構文、静的セマンティクス、実行時セマンティクスを定義する。

構文は、セクション 3 で定義された終端記号を組み込んだ ABNF [[RFC4234]](https://go.microsoft.com/fwlink/?LinkId=90462) 文法を用いて記述される。`<LINE-START>` と `<LINE-END>` 要素を明示的に識別する場合を除き、この文法はモジュール本体のソースコードを含むファイルの物理的な行構造を無視する。この文法は、セクション 3.4 で説明するように、条件付きコンパイル指示文と条件付き除外ソースコードも無視する。この文法は前処理されたモジュール本体（セクション 3.4）に適用され、ソースコードは字句トークン化と条件付きコンパイルの両方の前処理が適用されたかのように解釈される。この前処理は本仕様を単純化し明確にするためにのみなされる。実装が実際にこのような処理モデルを使用する必要はない。

## 5.1 モジュール本体の構造

```
procedural-module-body = LINE-START  procedural-module-declaration-section
                         LINE-START  procedural-module-code-section

class-module-body = LINE-START  class-module-declaration-section
                    LINE-START  class-module-code-section
```

プロシージャモジュール（セクション 4.2）とクラスモジュール（セクション 4.2）はともに、宣言部（セクション 5.2）とコード部（セクション 5.3）の二つの部分からなるモジュール本体（セクション 4.2）を持っている。各部分は、それを含むソースファイルの物理的な行の最初の構文要素として<ins>出現しなければならない</ins>。

本仕様では、様々な形式のエンティティ（セクション 2.2）名を表現するために以下の共通文法規則を使用する。

```
unrestricted-name = name / reserved-identifier
name = untyped-name / TYPED-NAME
untyped-name = IDENTIFIER / FOREIGN-NAME
```

## 5.2 モジュール宣言部の構造

モジュール（セクション 4.2）の宣言部は、ディレクティブと宣言から構成される。一般的に、ディレクティブはモジュール内の静的セマンティクスの適用を制御する。宣言はプログラムの実行環境内に存在する名前付きエンティティを定義する。

```
procedural-module-declaration-section = [*(procedural-module-directive-element EOS) def-directive]  *( procedural-module-declaration-element EOS)
class-module-declaration-section = [*(class-module-directive-element EOS) def-directive]  *(class-module-declaration-element EOS)
procedural-module-directive-element = common-option-directive / option-private-directive / def-directive 
procedural-module-declaration-element = common-module-declaration-element / global-variable-declaration / public-const-declaration / public-type-declaration / public-external-procedure-declaration / global-enum-declaration / common-option-directive / option-private-directive

class-module-directive-element = common-option-directive / def-directive / implements-directive
class-module-declaration-element = common-module-declaration-element / event-declaration / commonoption-directive / implements-directive 
```

静的セマンティクス

モジュール宣言部の中でのディレクティブと宣言の出現回数と相対的な順序には様々な制限がある。これらの制限は、個々のディレクティブと宣言の要素の定義の一部として指定される。

### 5.2.1 Option ディレクティブ

`Option` ディレクティブは、様々な言語機能の代替セマンティクスを選択するために使用される。

```
common-option-directive = option-compare-directive /  option-base-directive / option-explicit-directive  / rem-statement
```

静的セマンティクス

- 各 `<common-option-directive>` は、各 `<procedural-module-declaration-section>` または `<class-module-declaration-section>` に最大 1 回まで出現することが可能である。
- `<option-private-directive>` は、各 `<procedural-module-declaration-section>` に最大 1 回まで出現することが可能である。

#### 5.2.1.1 Option Compare ディレクティブ

`Option Compare` ディレクティブは、関係演算子（セクション 5.6.9.5）がモジュール（セクション 4.2）内の文字列データ値（セクション 2.1）に適用される際の比較規則を決定する。これはモジュールの比較モードとして知られている。

```
option-compare-directive = "Option"   "Compare"   ( "Binary" / "Text")
```

静的セマンティクス

- `<option-compare-directive>` が `Binary` キーワード（セクション 3.3.5.1）を含んでいる場合、モジュールの比較モードはバイナリ比較モードになる。
- `<option-compare-directive>` が `Text` キーワードを含んでいる場合、モジュールの比較モードはテキスト比較モードになる。
- `<option-compare-directive>` は `<procedural-module-declaration-section>` や `<class-module-declaration-section>` 内に 1 つだけ存在できる。
- `<procedural-module-declaration-section>` または `<class-module-declaration-section>` が `<option-compare-directive>` を含んでいない場合、そのモジュールの比較モードはバイナリ比較モードとなる。

#### 5.2.1.2 Option Base ディレクティブ

`Option Base` ディレクティブは、モジュール（セクション 4.2）で使用する、`<dim-spec>` の `<lower-bound>` で明示的に指定されていないすべての配列次元の下限（セクション 2.1）にデフォルト値を設定する。

```
option-base-directive = "Option"   "Base"     INTEGER
```

静的セマンティクス

- `<option-base-directive>` は `<procedural-module-declaration-section>` や `<class-module-declaration-section>` 内に最大 1 回まで出現することが可能である。
- もし、`<option-base-directive>` があるならば、同じ `<procedural-module-declaration-section>` や `<class-module-declaration-section>` 内で最初に現れる `<dim-spec>` の前に<ins>なければならない</ins>。
- `<INTEGER>` のデータ値（セクション 2.1）は、整数データ値 0 または 1 のどちらかに<ins>等しくなければならない</ins>。
- それを含むモジュールにおける配列次元のデフォルトの下限値は `<INTEGER>` 要素のデータ値となる。
- `<procedural-module-declaration-section>` や `<class-module-declaration-section>` に `<option-base-directive>` がない場合、モジュール内の配列次元の下限のデフォルト値は 0 となる。

#### 5.2.1.3 Option Explicit ディレクティブ

`Option Explicit` ディレクティブは、変数（セクション 2.3）を包含するモジュール（セクション 4.2）内で暗黙的に宣言（セクション 5.6.10）できるかどうかを制御する変数宣言モードを設定するために使用される。

```
option-explicit-directive = "Option"   "Explicit"
```

静的セマンティクス

- モジュール内に `<option-explicit-directive>` が存在する場合、そのモジュールの変数宣言モードは明示的モードとなる。
- `<option-explicit-directive>` がモジュール内に存在しない場合、モジュールの変数宣言モードは暗黙的モードとなる。
- `<option-explicit-directive>` は `<procedural-module-declaration-section>` または `<class-module-declaration-section>` の中に 1 つだけ存在することが可能である。
- `<procedural-module-declaration-section>` または `<class-module-declaration-section>` に `<option-explicit-directive>` がない場合、そのモジュールの変数宣言モードは暗黙的モードとなる。

#### 5.2.1.4 Option Private ディレクティブ

`Option Private` ディレクティブは、モジュール（セクション 4.2）の他のプロジェクト（セクション 4.1）に対するアクセス制限を制御し、モジュール内で宣言された `Public` エンティティ（セクション 2.2）の公開アクセスの意味も制御する。

```
option-private-directive = "Option"   "Private"   "Module"
```

静的セマンティクス

- プロシージャモジュール（セクション 4.2）に `<option-private-directive>` が含まれている場合、そのモジュール自身は非公開モジュールとみなされ、そのプロジェクト内でのみアクセス可能である。
- プロシージャモジュールに `<option-private-directive>` が含まれていない場合、そのモジュール自体は公開モジュールとみなされ、そのプロジェクト内およびそのプロジェクトを参照しているすべてのプロジェクト内でアクセス可能である。
- モジュールのアクセス制限がモジュール内の宣言のアクセス制限に及ぼす影響については、セクション 5.2.3 の特定のモジュール宣言形式の定義で説明されている。


### 5.2.2 暗黙定義ディレクティブ
```
def-directive = def-type  letter-spec *( "," letter-spec)
letter-spec = single-letter /  universal-letter-range / letter-range

single-letter = IDENTIFIER   ; %x0041-005A / %x0061-007A

universal-letter-range = upper-case-A "-"upper-case-Z
upper-case-A = IDENTIFIER
upper-case-Z = IDENTIFIER

letter-range = first-letter  "-" last-letter
first-letter = IDENTIFIER
last-letter = IDENTIFIER

def-type = "DefBool" / "DefByte" / "DefCur" /  "DefDate" / "DefDbl" / "DefInt" / "DefLng" / "DefLngLng" / "DefLngPtr" / "DefObj" / "DefSng" / "DefStr" / "DefVar"
```

暗黙的定義ディレクティブは、暗黙的に型付けされたエンティティ（セクション 2.2）の宣言型（セクション 2.2）を決定するために、モジュール（セクション 4.2）内で使用される規則を定義する。このようなエンティティの宣言型は、その名称値（セクション 3.3.5.1）の最初の文字に基づいて決定される。暗黙定義ディレクティブは、そのような文字から宣言型への対応を定義する。

静的セマンティクス

- `<single-letter>` の `<IDENTIFIER>` 要素の名前は、1 つの大文字または小文字のアルファベット（%x0041-005A または %x0061-007A）で<ins>構成されなければならない</ins>。
- `<upper-case-A>` の `<IDENTIFIER>` 要素の名称値は、"A" (%x0041) 1 文字で<ins>構成されなければならない</ins>。
- `<upper-case-Z>` の `<IDENTIFIER>` 要素の名称値は、"Z" (%x005A) 1 文字で<ins>構成されなければならない</ins>。
- `<single-letter>` からなる `<letter-spec>` は、`<single-letter>` の `<IDENTIFIER>` 要素の名称値である文字で始まるすべての `<IDENTIFIER>` トークンの、モジュール内での暗黙宣言型を定義している。

- `<letter-range>` からなる `<letter-spec>`は、`<IDENTIFIER>` トークンをもつすべてのエンティティでその名称値が最初の文字を含む `<first-letter>` `<IDENTIFIER>` 要素の名称値であり、最後の文字を含む `<last-letter>` `<IDENTIFIER>` 要素の名称値である連続する文字の範囲のいずれかの文字から始まっているもので、モジュール内での宣言型を暗黙的に定める。連続する文字の範囲とは、文字の昇順または降順の範囲であり、1 文字で構成することができる。
- `<procedural-module-declaration-section>` または `<class-module-declaration-section>` 内では、`<letter-spec>` プロダクションの間で重複が許されない。
- `<universal-letter-range>` は、モジュール内のすべての `<IDENTIFIER>` に対して単一の宣言型を暗黙的に定義する。

各 `<def-type>` に対応する宣言型は、以下の表で定義される。

| `<def-type>` | 宣言型 |
| ---- | ---- |
| "DefBool" | Boolean |
| "DefByte" | Byte |
| "DefInt" | Integer |
| "DefLng" | Long |
| "DefLngLng" | LongLong |
| "DefLngPtr" | LongPtr の別名 |
| "DefCur" | Currency |
| "DefSng" | Single |
| "DefDbl" | Double |
| "DefDate" | Date |
| "DefStr" | String |
| "DefObj" | オブジェクト参照 |
| "DefVar" | Variant |

エンティティが明示的に型付けされておらず、適用可能な `<def-type>` が存在しない場合、エンティティの宣言型は `Variant` となる。

### 5.2.3 モジュール宣言部

```
common-module-declaration-element = module-variable-declaration
common-module-declaration-element =/ private-const-declaration
common-module-declaration-element =/ private-type-declaration
common-module-declaration-element =/ enum-declaration
common-module-declaration-element =/ private-external-procedure-declaration
```

どのような種類のモジュール（セクション 4.2）でも `<common-module-declaration-element>` を含むことができる。それ以外の宣言は `<procedural-module>` か `<class-module>` のどちらか一方に固有のものである。

#### 5.2.3.1 モジュール宣言の変数リスト

```
module-variable-declaration = public-variable-declaration / private-variable-declaration

global-variable-declaration = "Global"  variable-declaration-list
public-variable-declaration = "Public" ["Shared"] module-variable-declaration-list
private-variable-declaration = ("Private" / "Dim") ["Shared"] module-variable-declaration-list

module-variable-declaration-list = (withevents-variable-dcl / variable-dcl)
      *( "," (withevents-variable-dcl  / variable-dcl) )
variable-declaration-list = variable-dcl *( "," variable-dcl )
```

`<global-variable-declaration>` とオプションの `Shared` キーワード（セクション 3.3.5.1）は、他の Basic 言語の方言や VBA の古いバージョンとの構文互換性を提供する。

静的セマンティクス

- `Shared` キーワードは意味を持たない。
- 同じモジュール（セクション 4.2）に含まれる `<module-variable-declaration>` で定義された各変数（セクション 2.3）は、異なる変数名（セクション 2.3）を<ins>持たなければならない</ins>。
- `<module-variable-declaration>` 内で定義される各変数はモジュール変数であり、同じモジュール内で定義される他のモジュール変数、モジュール定数、列挙体メンバー、プロシージャ（セクション 2.4）の名前と異なる変数名を<ins>持たなければならない</ins>。
- `<global-variable-declaration>` または `<public-variable-declaration>` の一部である変数宣言は、公開変数を宣言している。この変数は、プロジェクト（セクション 4.1）内でアクセス可能である。それを包含するモジュールがクラスモジュール（セクション 4.2）、または非公開モジュール（セクション 5.2.1.4）ではないプロシージャモジュール（セクション 4.2）の場合、変数はそれを包含するプロジェクトを参照しているプロジェクト内でもアクセス可能である。
- `<private-variable-declaration>` の一部である変数宣言は、非公開変数を宣言している。その変数はそのモジュール内でのみアクセス可能である。
- `<public-variable-declaration>` で定義された変数の変数名がプロジェクト名（セクション 4.1）やモジュール名（セクション 4.2）と同じ場合、変数名へのすべての参照は `<public-variable-declaration>` を含むモジュール内で発生しない限りモジュール修飾として<ins>扱われなければならない</ins>。
- `<module-variable-declaration>` で定義された変数は、同じモジュールで定義された `<enum-declaration>` の列挙型名と同じ変数名を持つことができるが、変数名がモジュール修飾されていてもその変数名を用いて参照することはできない。
- `<public-variable-declaration>` で定義された変数が、異なるモジュールの公開されている `<enum-declaration>` の列挙型名と同じ変数名を持つ場合、その変数名への参照は `<public-variable-declaration>` を含むモジュール内で発生しない限りすべてモジュール修飾で<ins>なければらない</ins>。
- `<class-module-code-section>` の `<public-variable-declaration>` で定義された変数の宣言型（セクション 2.2）は、`<private-type-declaration>` や 非公開の列挙型名で定義された UDT（セクション 2.1）でない場合がある。
- プロシージャモジュールに含まれる `<module-variable-declaration-list>` は、`<withevents-variable-dcl>` 要素を<ins>含んではならない</ins>。

実行時セマンティクス

- `<procedural-module-declaration-section>` の要素である `<module-variable-declaration>` で定義された変数は、すべてモジュールエクステント（セクション 2.3）を持っている。
- `<class-module-declaration-section>` の要素である `<module-variable-declaration>` で定義された変数は、クラスのメンバ変数（セクション 2.5）であり、オブジェクトエクステント（セクション 2.3）を持つ。クラスの各インスタンス（セクション 2.5）は対応する個別の変数を含むことになる。
